buildscript {
    ext.kotlin_version = '1.1.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin-platform-common'

allprojects {
    apply plugin: 'maven-publish'

    group 'org.test'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

}

dependencies {
    // multitarget library dependency
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
}

project('jre6') {
    apply plugin: 'kotlin-platform-jvm'

    dependencies {
        implement project(":commonlib")
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "joda-time:joda-time:2.9.2"
    }
    compileKotlin {
        kotlinOptions.jdkHome = System.getenv("JDK_16")
    }
}

project('js') {
    apply plugin: 'kotlin-platform-js'

    dependencies {
        implement project(":commonlib")
        compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    }

    def outDir = "${buildDir}/kotlin2js/main/"
    compileKotlin2Js {
        kotlinOptions.outputFile = outDir + "commonlib.js"
    }
    def testOutDir = "${buildDir}/kotlin2js/test/"
    compileTestKotlin2Js {
        kotlinOptions.outputFile = testOutDir + "test.js"
    }
}

subprojects {
    archivesBaseName = "${parent.name}-${project.name}"

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }
    publishing {
        publications {
            mainPublication(MavenPublication) {
                artifactId archivesBaseName
                from components.java
                artifact sourcesJar

                pom.withXml {
                    asNode().dependencies.'*'.each { it.scope*.value = 'compile'}
                }
            }
        }
    }
}




task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}
publishing {
    publications {
        mainPublication(MavenPublication) {
            artifactId archivesBaseName
            from components.java
            artifact sourcesJar

            pom.withXml {
                asNode().dependencies.'*'.each { it.scope*.value = 'compile'}
            }
        }
    }
}